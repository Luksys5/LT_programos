#!/bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------
#$Author: grazulis $
#$Date: 2000/05/08 14:03:00 $ 
#$Locker:  $
#$Revision: 1.5 $
#$Source: /home/grazulis/src/tdb/RCS/ovid2tdb,v $
#$State: Exp $
#------------------------------------------------------------------------
#*
# Convert Ovid search reports to .tdb (tagged database) format
#**

use strict;

sub fold
{
    my $length = shift;
    my $separator = shift;
    my $string = shift;
    my @lines = ();
    my $line = "";

    my $ors = $separator =~ /\s/ ? $separator : "$separator ";
    my $word;
    for $word (split( $separator, $string )) {
	$word =~ s/^\s*|\s*$//g;
	if( !$line ) {
	    $line = $word;
	} else {
	    my $new_line = "$line$ors$word";
	    if( length($new_line) < $length ) {
		$line = $new_line;
	    } else {
		push( @lines, $line );
		$line = $word;
	    }
	}
    }
    push( @lines, $line );
    return @lines;
}

my $mode = "";

my $authors = "";
my $title = "";
my $source = "";

my $journal = "";
my $no = "";
my $vol = "";
my $pages = "";
my $year = "";

while(<>) {
    if( /^(\w.*)$/ ) { $mode = $1; next }
    if( /^\s/ ) {
	chomp;
	s/^\s+//g;
	if( $mode =~ /^Author(s?)/ ) {
	    s/\./\.;/g;
	    s/(\w+)(\s+)([A-Z]+\.)/$1,$2$3/g;
	    s/([A-Z]+)\./join( ". ", split( "", $1 ))."."/eg;
	    $authors = $authors ? "$authors $_" : $_;
	}
	if( $mode eq "Title" ) { $title = $title ? "$title $_" : $_ }
	if( $mode eq "Source" ) {
	    $source = $source ? "$source $_" : $_;
	    if( $source =~ /([^\.]+)\.\s*(\d+) \(([-\d]+)\) : 
	       ([-\d]+),\s*(\d+)\s*(.+)$ /x ) {
		$journal = $1;
		$vol = $2;
		$no = $3;
		$pages = $4;
		$year = $5;
	    }
	    elsif( $source =~ /([^\.]+)\.\s*(\d+) :
	       ([-\d]+),\s*(\d+)\s*(.+)$ /x ) {
		$journal = $1;
		$vol = $2;
		$pages = $3;
		$year = $4;
	    }
	    elsif( $source =~ /([^\.]+)\.\s*\((\d+)\) :
	       ([-\d]+),\s*(\d+)\s*(.+)$ /x ) {
		$journal = $1;
		$no = $2;
		$pages = $3;
		$year = $4;
	    }
	}
    }
    if( (/^\s*$/ or eof) and $title ) {
	print "NR\t...\n";
	print map {"AU\t$_\n"} fold( 70, ";", $authors );
	print map {"TI\t$_\n"} fold( 70, " ", $title );
	if( !$journal ) {
	    print map {"JO\t$_\n"} fold( 70, " ", $source );
	} else {
	    print map {"JO\t$_\n"} fold( 70, " ", $journal );
	    print map {"YE\t$_\n"} fold( 70, " ", $year );
	    print map {"NO\t$_\n"} fold( 70, " ", $no ) if $no;
	    print map {"VO\t$_\n"} fold( 70, " ", $vol ) if $vol;
	    print map {"PA\t$_\n"} fold( 70, " ", $pages );
	}
	print "\n";
	$authors = $title = $source = "";
	$journal = $no = $vol = $pages = $year = "";
	next;
    }
}
