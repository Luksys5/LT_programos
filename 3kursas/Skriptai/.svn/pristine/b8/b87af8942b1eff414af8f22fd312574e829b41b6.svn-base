#!/bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------
#$Author: saulius $
#$Date: 2000/09/10 08:47:16 $ 
#$Locker:  $
#$Revision: 1.12 $
#$Source: /home/saulius/src/tdb/RCS/tdb2bibtex,v $
#$State: Exp $
#------------------------------------------------------------------------
#*
# Convert a tagged data base (.tdb) with literature references to the 
# BibTeX format.
#**

use strict;

my %other_fields = (
    "AD"   => "address",
    "ADDR" => "address",
    "OR"   => "organisation",
    "MO"   => "month",
    "CH"   => "chapter",
    "TYP"  => "type",
    "EDN"  => "edition",
    "SE"   => "series",
    "BO"   => "booktitle",
);

sub fold
{
    my $length = shift;
    my $separator = shift;
    my $string = shift;
    my @lines = ();
    my $line = "";

    my $ors = $separator =~ /\s/ ? $separator : "$separator ";
    my $word;
    for $word (split( $separator, $string )) {
	$word =~ s/^\s*|\s*$//g;
	if( !$line ) {
	    $line = $word;
	} else {
	    my $new_line = "$line$ors$word";
	    if( length($new_line) < $length ) {
		$line = $new_line;
	    } else {
		push( @lines, $line );
		$line = $word;
	    }
	}
    }
    push( @lines, $line );
    return @lines;
}

my @articles = ();
my $new_article = 0;
my @preamble = ();
my @commands = ();

while(<>) {
    if( /^\s*#=?BIBTEX_(COMMAND|PREAMBLE)/ ) {
       chomp;
       my ($kwd, $text) = split( " ", $_, 2 );
       push( @commands, $text ) if /^\s*#=BIBTEX_COMMAND/;
       push( @preamble, $text ) if /^\s*#=BIBTEX_PREAMBLE/;
       next;
    }
    if( /^\s*#=/ ) { s/^(\s*)#/$1%/; print; next }
    if( /^\s*#/ ) { next }
    if( /^\s*$/ ) {
	if( $new_article ) {
	    push( @articles, {} );
	    $new_article = 0;
	}
	next;
    }
    $new_article = 1;
    if( !@articles ) { @articles = ({}) }
    chomp;
    my ($key, $value) = split( " ", $_, 2 );
    ## $value =~ s/([^\\])\$/$1\\\$/g;
    ## $value =~ s/^\$/\\\$/g;
    $value =~ s/([^\\])&/$1\\&/g;
    $value =~ s/([^\\])%/$1\\%/g;
    $value =~ s/^&/\\&/g;
    $value =~ s/^%/\\%/g;
    if( $key eq "AU" ) {
	$articles[-1]{$key} = !defined $articles[-1]{$key} ?
	    $value : "$articles[-1]{$key}; $value";
    } else {
	$articles[-1]{$key} = !defined $articles[-1]{$key} ?
	    $value : "$articles[-1]{$key} $value";
    }
}

pop( @articles ) if $new_article == 0;

for (@preamble) { print "$_\n" }

for (@commands) {
    print "$_\n";
}
print "\n" if @commands;

for(@articles) {

    my $authors = $_->{AU};
    my $editor = $_->{ED};
    my ($first_author) = split(";",
			       (defined $authors ? $authors : $editor), 2 );

    $first_author =~ s/^\s+|\s+$//g;
    $first_author =~ s/[^\w\s\.\,]//g;
    if( $first_author =~ m/(\w+),/ ) {
	$first_author = $1;
    } elsif( $first_author =~ m/(\w+)$/ ) {
	$first_author = $1;
    } else {
	$first_author =~ s/\w+\.//g;
	$first_author =~ s/[^\w]//g;
    }

    $authors =~ s/;/ and/g if defined $authors;
    $editor =~ s/;/ and/g if defined $editor;

    my $type = defined $_->{TY} ? lc($_->{TY}) : "";
    $type = "article" unless $type;
    $type = "article" if $type =~ /journal article/i;
    $type = "inbook"  if $type =~ /book section/i;
    $type =~ s/\s//g;

    my $page = $_->{PA};
    $page =~ s/-.*$// if defined $page;
    $page = "" if !defined $page and $type ne "article";

    my $nr = (defined $_->{NR} and $_->{NR} ne "...") ? "$_->{NR}_" : "";
    my $year = defined $_->{YE} ? $_->{YE} : "XXXX";
    my $texid;

    if( defined $_->{KEY} ) {
	$texid = $_->{KEY};
    } else {
	$texid = $page ? 
	    "${nr}${first_author}_${year}_${page}" :
	    "${nr}${first_author}_${year}";
	$texid .= $_->{KADD} if defined $_->{KADD};
    }

    if( !defined $editor and !defined $authors ) {
	print STDERR "$0: warninig: reference '$texid' " .
	    "has neither author nor editor\n";
    }

    my ( $id1, $id2 ) = ( " "x4, " "x12 );

    print "\@$type\{ $texid,\n";

    print "${id1}author  = { ",
          join( "\n${id1}${id2}", fold( 60, " ", $authors )), " },\n"
	      if defined $authors;

    print "${id1}editor  = { ",
          join( "\n${id1}${id2}", fold( 60, " ", $editor )), " },\n"
	      if defined $editor;

    $_->{TI} =~ s/(^|[^\\\{a-zA-Z])([A-Z]+)/$1\{$2\}/g;

    print "${id1}title   = { ",
          join( "\n${id1}${id2}", fold( 60, " ", $_->{TI} )), " },\n";

    print "${id1}journal = { ",
          join( "\n${id1}${id2}", fold( 60, " ", $_->{JO} )), " },\n"
	      if defined $_->{JO};

    print "${id1}publisher = { ",
          join( "\n${id1}${id2}", fold( 60, " ", $_->{PU} )), " },\n"
	      if defined $_->{PU};

    if( defined $_->{YE} ) {
	print "${id1}year    = { $_->{YE} },\n";
    } else {
	print STDERR "$0: warning: year undefined for citation '$texid'\n";
    }

    if( defined $_->{VO} ) {
	print "${id1}volume  = { $_->{VO} },\n";
    }

    if( defined $_->{NO} ) {
	print "${id1}number  = { $_->{NO} },\n";
    }

    my $field;
    for $field ( keys %other_fields ) {
	if( defined $_->{$field} ) {
	    print "${id1}$other_fields{$field}  = { $_->{$field} },\n";
	}
    }

    print "${id1}pages   = { $_->{PA} },\n" if $page;

    print "}\n\n";
}
