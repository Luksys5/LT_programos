#! /bin/sh
#!perl -w # --*- Perl -*--
eval 'exec perl -x $0 ${1+"$@"}'
    if 0;
#------------------------------------------------------------------------------
#$Author: andrius $
#$Date: 2015-01-29 15:00:29 +0200 (Kt, 29 Sau 2015) $ 
#$Revision: 481 $
#$URL: svn://saulius-grazulis.lt/scripts/help2man $
#------------------------------------------------------------------------------
#*
#  Generate man page for a script, marked up according to SOptions Perl
#  module.
#**

use strict;
use warnings;
use Capture::Tiny ':all';
use File::Basename;
use SOptions;
use SUsage;

my $bugs_url;
my $bugs_email;

my $format = 'man';

@ARGV = getOptions(
#* Usage:
#*     $0 --options input.cif inputs*.cif
#*
#* Options:
#*     --man
#*          Generate a man page. Default option.
#*
#*     --wiki
#*          Generate a wiki page.
#*
#*     --bugs-url
#*          Specify Web URL for bug reporting.
#*
#*     --bugs-email
#*          Specify e-mail address for bug reporting.
#*
#*     --help  print a short usage message (this message) and exit.
#**
    "--man"  => sub { $format = 'man' },
    "--wiki" => sub { $format = 'wiki' },

    "--bugs-url"   => \$bugs_url,
    "--bugs-email" => \$bugs_email,

    "--help,--usage" => sub { SUsage::usage; exit },
);

for my $filename (@ARGV) {
    my $name = basename( $filename );
    my( $help, $errors ) = capture {
        local $0 = $name;
        SUsage::usage( $filename );
    };
    die $errors if $errors;

    my @help = split( "\n", $help );

    my @head;
    my @usage;
    my @options;

    my %flags = ( in_head => 1 );
    while( @help ) {
        $_ = shift @help;

        if(    /^\s*usage:\s*$/i ) {
            %flags = ( in_usage => 1 );
            next;
        }
        elsif( /^\s*options:\s*$/i ) {
            %flags = ( in_options => 1 );
            next;
        }

        if(    exists $flags{in_head} ) {
            s/^\s+//;
            push( @head, $_ );
        }
        elsif( exists $flags{in_usage} ) {
            s/^\s+//;
            push( @usage, $_ );
        }
        elsif( exists $flags{in_options} ) {
            if( /^\s*\-\S/ && @options
                && $options[-1] =~ /^\s*\-\S/ ) {
                push( @options, "" );
            }
            push( @options, $_ );
        }
    }

    my $indent_length;
    foreach( @options ) {
        next if $_ eq "";
        /^(\s*)/;
        if( !defined $indent_length ||
            $indent_length > length( $1 ) ) {
            $indent_length = length( $1 );
        }
    }

    @options = map { $_ ne ""
                            ? substr( $_, $indent_length )
                            : "" } @options;

    my $options;
    if( $format eq 'man' ) {
        @head    = map { s/\-/\\-/g; $_ } @head;
        @usage   = map { s/\-/\\-/g; $_ } @usage;
        @options = map { s/\-/\\-/g; $_ } @options;
        $options = join "\n", @options;
    } elsif( $format eq 'wiki' ) {
        my @options_now = (";");
        my $last_line;
        foreach( grep { !/^$/ } @options ) {
            if( !defined $last_line ) {
                $last_line = $_;
                next;
            }
            if( /^-/ ) {
                if( $last_line =~ /^-/ ) {
                    push @options_now, "$last_line ";
                } else {
                    push @options_now, "$last_line\n";
                    push @options_now, ";";
                }
            } else {
                s/^\s+//;
                if( $last_line =~ /^-/ ) {
                    push @options_now, "$last_line\n";
                    push @options_now, ":";
                } else {
                    push @options_now, "$last_line ";
                }
            }
            $last_line = $_;
        }
        push @options_now, $last_line;
        $options = join "", @options_now;
    } else {
        die "$0: unknown format: '$format'"
    }

    my $head    = join "\n", @head;
    my $usage   = join "\n", @usage;

    $head    = "to be described..." unless $head;
    $usage   = "$name [options] [input files]" unless $usage;
    $options = "to be described..." unless $options;

    if( $format eq 'man' ) {
        print <<END;
.TH $name
.SH NAME
$name
.SH SYNOPSIS
$usage
.SH DESCRIPTION
$head
.SH OPTIONS
$options
END
    } elsif( $format eq 'wiki' ) {
        $head =~ s/^\s*//;
        $head = lcfirst( $head );
        print <<END;
'''$name''' -- $head
== Synopsis ==
  $usage
== Options ==
$options
END
    } else {
        die "$0: unknown format: '$format'"
    }

    my @bugs_contacts;
    push( @bugs_contacts, "Report $name bugs using Web: $bugs_url" )
        if defined $bugs_url;
    push( @bugs_contacts, "Report $name bugs using e-mail: $bugs_email" )
        if defined $bugs_email;

    if( @bugs_contacts > 0 ) {
        if( $format eq 'man' ) {
            print ".SH \"REPORTING BUGS\"\n" .
                  join( "\n.br\n",
                        map { s/\-/\\-/g; $_ }
                            @bugs_contacts ) . "\n";
        } elsif( $format eq 'wiki' ) {
            print "== Reporting bugs ==\n" . join( "\n\n", @bugs_contacts );
        } else {
            die "$0: unknown format: '$format'"
        }
    }
}
